plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'com.diffplug.spotless' version "$spotlessVersion"

}

apply plugin: 'io.spring.dependency-management'

group = 'org.glodean'
version = "$appVersion"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:$webfluxUIVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-redis:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-webflux:$springBootVersion"
    implementation "org.apache.solr:solr-solrj:$solrjVersion"
    implementation "org.apache.logging.log4j:log4j-api:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-core:$log4jVersion"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}
tasks.named('bootJar'){
    mainClass.set("$entryClass")
}

tasks.named('bootRun'){
    mainClass.set("$entryClass")
}

jacocoTestReport {
    dependsOn test // Ensure tests run before generating reports
    reports {
        xml.required.set(true) // Enable XML report (useful for CI tools)
        html.required.set(true) // Enable HTML report for local viewing
        html.outputLocation.set(layout.buildDirectory.dir('jacocoHtml'))
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // Generate report after tests
}

jacoco {
    toolVersion = "0.8.13" // Specify the JaCoCo version
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

spotless {
    java {
        // Use Google's standard Java format (same as used in the JDK)
        googleJavaFormat('1.17.0')
        // Include all your source files
        target 'src//*.java'
        importOrder()
        removeUnusedImports()
    }
}
